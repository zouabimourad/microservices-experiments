buildscript {

	ext {
		springBootVersion = '2.0.7.RELEASE'
	}

	repositories {
		mavenCentral()
	}

	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("se.transmode.gradle:gradle-docker:1.2")
	}

}

subprojects {

	apply plugin: 'java'
	apply plugin: 'maven-publish'
	apply plugin: "io.spring.dependency-management"

	group = 'com.typesafe.microservices-experiments'
	version = '0.0.1-SNAPSHOT'

	sourceCompatibility = 1.8

	repositories {
		mavenCentral()
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
		}
	}

	publishing {
		publications {
			maven(MavenPublication) {
				from components.java
			}
		}

		repositories{
			mavenLocal()
		}
	}

}

configure([project(':order-service'),
		   project(':product-service'),
		   project(':account-service'),
		   project(':zuul'),
		   project(':eureka'),
		   project(':hystrix-dashboard')]) {

	apply plugin: 'org.springframework.boot'
	apply plugin: "docker"

	ext {
		springCloudVersion = 'Finchley.RELEASE'
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		}
	}

	task buildDocker(type: Docker, dependsOn: build) {

		def projectName = "microservices-experiments"
		def jarName = jar.baseName.toLowerCase()
		maintainer = "zouabi.mourad@gmail.com"

		tag = "$projectName/$jarName"
		dockerfile = file('./Dockerfile')
		doFirst {
			copy {
				from jar
				into stageDir
			}
		}
	}

}